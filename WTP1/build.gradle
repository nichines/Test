apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'

ext.servletApiVersion = '3.0.1'

repositories {
    mavenCentral()
}

dependencies {
    providedCompile "javax.servlet:javax.servlet-api:$servletApiVersion"
}

eclipse {
	project {
		natures += "org.eclipse.wst.jsdt.core.jsNature"
		buildCommands += [new org.gradle.plugins.ide.eclipse.model.BuildCommand("org.eclipse.wst.jsdt.core.javascriptValidator")]
	}

	classpath {
		defaultOutputDir = file('build/main/classes')

		file {
			whenMerged { classpath ->
  				classpath.entries.findAll { it.path.startsWith("src/test/") }*.output = "build/test/classes"
  			}
		}
	}

	wtp {
		component { // org.eclipse.wst.common.component

			//<wb-resource deploy-path="/" source-path="/war" tag="defaultRootSource"/>
			resource sourcePath: "war", deployPath: "/", tag: "defaultRootSource"
		}

		facet { // org.eclipse.wst.common.project.facet.core.xml

			facet name: 'java', version: '1.7'
			facet name: 'jst.web', version: '3.0'
			facet name: 'wst.jsdt.web', version: '1.0'
		}
	}
}
